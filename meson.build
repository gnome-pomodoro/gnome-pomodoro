project(
  'gnome-pomodoro',
  ['vala', 'c'],
  version: '1.0-alpha',
  meson_version: '>=0.59.0',
  default_options: [
    'buildtype=debugoptimized',
    'warning_level=2',
  ],
)

i18n = import('i18n')
gnome = import ('gnome')

glib_dep = dependency('glib-2.0', version: '>=2.50')
gobject_dep = dependency('gobject-2.0', version: '>=2.50')
gio_dep = dependency('gio-2.0', version: '>=2.50')
gtk_dep = dependency('gtk4', version: '>=4.2')
cairo_dep = dependency('cairo')
graphene_dep = dependency('graphene-gobject-1.0')
libadwaita_dep = dependency('libadwaita-1', version: '>= 1.0.0')
introspection_dep = dependency('gobject-introspection-1.0', version: '>=0.10.1')
peas_dep = dependency('libpeas-1.0', version: '>=1.5.0')
gom_dep = dependency('gom-1.0', version: '>=0.3.0')
gsound_dep = dependency('gsound', version: '>=0.98')
gstreamer_dep = dependency('gstreamer-1.0', version: '>=1.0.10')
json_dep = dependency('json-glib-1.0', version: '>=1.6.2')
sqlite_dep = dependency('sqlite3')
pangocairo_dep = dependency('pangocairo')
gtk_x11_dep = dependency('gtk4-x11', required: false)
gtk_wayland_dep = dependency('gtk4-wayland', required: false)

cc = meson.get_compiler('c')
libm_dep = cc.find_library('m')
valac = meson.get_compiler('vala')
posix_dep = valac.find_library('posix')

check_headers = [
  'sys/types.h',
  'unistd.h',
  'langinfo.h',
  'locale.h'
]

foreach h : check_headers
  cc.has_header(h, required: true)
endforeach

add_project_arguments(
  [
    '-DGETTEXT_PACKAGE="' + meson.project_name() + '"',
    '-DG_LOG_DOMAIN="' + meson.project_name() + '"',
  ],
  language: 'c',
)

# Ignore warnings for C code generated by Vala.
add_project_arguments(
  [
    '-DVALA_STRICT_C',
    '-Wno-error',
    '-Wno-discarded-qualifiers',
    '-Wno-incompatible-pointer-types',
    '-Wno-cast-function-type',
    '-Wno-unused-parameter',
    '-Wno-unused-label',
    '-Wno-unused-but-set-variable',
    '-Wno-missing-field-initializers',
    '-Wno-unused-variable',
    '-Wno-unused-function',
    '-Wno-sign-compare',
    '-Wno-address',
  ],
  language: 'c',
)

# Enable GNU-specific extensions to get access to additional locale-related
# functionality beyond what's available in the standard POSIX.
add_project_arguments('-D_GNU_SOURCE', language: 'c')

add_project_arguments(
  [
    '--vapidir', meson.current_source_dir(),
    '--vapidir', meson.current_source_dir() / 'vapi',
    '--pkg', 'config',
  ],
  language: 'vala',
)

if gtk_x11_dep.found()
  add_project_arguments(['-D', 'HAVE_GDK_X11'], language: 'vala')
endif

if gtk_wayland_dep.found()
  add_project_arguments(['-D', 'HAVE_GDK_WAYLAND'], language: 'vala')
endif

application_id = 'org.gnomepomodoro.Pomodoro'
package_name = meson.project_name()
package_version = meson.project_version()
package_website = 'https://gnomepomodoro.org'
package_issue_url = 'https://github.com/gnome-pomodoro/gnome-pomodoro/issues'
package_support_url = 'https://github.com/gnome-pomodoro/gnome-pomodoro/discussions'
package_datadir = get_option('prefix') / get_option('datadir') / meson.project_name()
package_localedir = get_option('prefix') / get_option('datadir') / 'locale'
gschema_dir = get_option('prefix') / get_option('datadir') / 'glib-2.0' / 'schemas'
gettext_package = package_name

conf = configuration_data()

conf.set_quoted(
  'APPLICATION_ID',
  application_id,
)
conf.set_quoted(
  'PACKAGE_LOCALE_DIR',
  package_localedir,
)
conf.set_quoted(
  'PACKAGE_DATA_DIR',
  package_datadir,
)
conf.set_quoted(
  'PACKAGE_NAME',
  package_name,
)
conf.set_quoted(
  'PACKAGE_VERSION',
  package_version,
)
conf.set_quoted(
  'PACKAGE_WEBSITE',
  package_website,
)
conf.set_quoted(
  'PACKAGE_ISSUE_URL',
  package_issue_url,
)
conf.set_quoted(
  'PACKAGE_SUPPORT_URL',
  package_support_url,
)
conf.set_quoted(
  'GETTEXT_PACKAGE',
  gettext_package,
)
conf.set_quoted(
  'GSETTINGS_SCHEMA_DIR',
  gschema_dir,
)
conf.set(
  'HAVE_ALTMON',
  cc.has_header_symbol('langinfo.h', 'ALTMON_1', args: '-D_GNU_SOURCE')
)

configure_file(
  output: 'config.h',
  configuration: conf,
)

# Include the config.h we just generated
config_h_dir = include_directories('.')


subdir('po')
subdir('data')
subdir('src')
subdir('tests')
